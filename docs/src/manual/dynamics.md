# Stochastic Dynamics
Stochastic dynamics studies the behaviour of buildings and systems and their changes over time when they are affected by random processes, or also called stochastic processes, such as earthquakes or wind loads. Stochastic processes are characterised by an inherent randomness which can not be described deterministically [li2009sdos](@cite). To study the frequency distribution and their amplitude of a stochastic process, the so-called power spectral density (PSD) function can be defined.

## Semi-empirical PSD functions
Semi-empirical PSD functions describe the distribution of power across frequencies, combining theoretical models with experimental data for improved accuracy. Well established models in earthquake engineering are, for instance, the Kanai-Tajimi PSD model [kanai1957semi,tajimi1960statistical](@cite) or the Clough-Penzien PSD model [clough1975structures](@cite). The Clough-Penzien PSD model $S^{CP}$ with frequency vector $\omega$ reads as follows
```math
    S^{CP}(\omega, S_0, \omega_{f}, \zeta_{f}, \omega_{g}, \zeta_{g}) = S_0 \cdot \frac{{\omega^4}}{{(\omega_{f}^2-\omega^2)^2+4  \zeta_{f}^2  \omega_{f}^2  \omega^2}} \cdot \frac{{\omega_{g}^4+4  \zeta_{g}^2  \omega_{g}^2  \omega^2}}{{(\omega_{g}^2-\omega^2)^2+4  \zeta_{g}^2  \omega_{g}^2  \omega^2}},
```
where the parameters $S_0$, $\omega_{f}$, $\zeta_{f}$, $\omega_{g}$ and $\zeta_{g}$ characterizing soil conditions.

## Stochastic Process Generation
The spectral representation method (SRM) [shinozuka1991simulation](@cite) can be utilised to generate realisations of stochastic processes $x(t)$ which are carrying the characteristics of a PSD function $S(\omega)$ in time domain. The stochastic character of these processes is represented by random variables $\varphi$. The spectral representation method reads as follows
```math
    x(t) = \sqrt{2} \sum \limits_{n=0}^{N_{\omega}-1}\sqrt{2 S(\omega_n) \Delta \omega} \cos(\omega_n t + \varphi),
```
where $\Delta \omega$ is the frequency increment, $\omega_n$ is the frequency at coordinate $n$, $t$ is the time vector and $N_{\omega}$ is the total number of frequency points.

Stochastic processes generated by SRM can be utilised in a Monte Carlo framework, for instance, in order to determine the structural reliability or other quantities of interest.

## PSD estimation
If a time signal, such as an earthquake, is given in time domain and shall be studied for their characteristics in frequency domain, the PSD estimation can be carried out. Although rigorous mathematical relationships between stochastic processes and the PSD function exist, estimation techniques are often necessary since an exact determination would require continuous signals of infinite length, which are not practical in real-world applications.

A frequently used estimator of the stationary PSD function from time records is the periodogram [li2009sdos](@cite). To transform a stationary stochastic process from time domain to frequency domain, the discrete Fourier transform (DFT) can be utilised. The periodogram is the squared absolute value of the DFT of the discrete time signal $x$ with data point index $n$, such as
```math
    \hat{S}_X(\omega_k) = \frac{\Delta t^2}{T} \left| \sum \limits_{n = 0}^{N_t-1} x_n e^{-2 \pi i k n / N} \right|^2,
```
where $\Delta t$ is the time discretisation, $T$ is duration of the record, $N_t$ is the total number of data points, $i$ is the imaginary unit and $k$ is the integer frequency for $\omega_k = \frac{2 \pi k}{T}$.

## Implementation

To follow the procedure of generating signals that approximate homogeneous gaussian processes, as presented in [shinozuka1991simulation](@cite), first we need to define parameters for the frequency domain of the PSD function.

```@example ShinozukaBenchmarkParameters
using UncertaintyQuantification # hide
N = 128                         # Number of terms
ω_u = 4π                        # Cut-off frequency
Δω = ω_u / N                    # Frequency discretisation size
ω = collect(0:Δω:(ω_u - Δω))    # Frequency discretisation vector
```
With the discretized frequency domain in $\omega$, let us create a PSD function simply by calling

```@example ShinozukaPSDestimation
sd = ShinozukaDeodatis(ω, 1, 1) # σ=1 b=1
```
here $\sigma=1$ defines the first parameter of the provided analytical PSD in [shinozuka1991simulation](@cite) and $b=1$ the second parameter. There are other PSD functions predefined, such as

```@example
cp = CloughPenzien(ω, 0.1, 0.8π, 0.6, 8π, 0.6)  # S_0=0.1, ω_f=0.8π, ζ_f=0.6, ω_g=8π, ζ_g=0.6
kt = KanaiTajimi(ω, 0.25, 5, 0.75)              # S_0=.25, ω_0=5, ζ=0.75
```
`CloughPenzien` and `KanaiTajimi` are semi-empirical PSD functions used to model ground motion for earthquake engineering applications, for reference see [kanai1957semi](@cite), [tajimi1960statistical](@cite) and [clough1975structures](@cite).

To go from the frequency characterisation, that is accomlished by generating the `ShinozukaDeodatis` object `sd`

```@example evaluatePSD
Sval = evaluate(sd)
```
, to the generation of signals in the time domain, we need to define the time domain.

```@example ShinozukaTimeDomain
T0 = 2π/Δω              # Total simulation time
Δt = 2π/(2*ω_u)         # Time step size
t = collect(Δt:Δt:(T0)) # Time discretisation vector
```
With the time domain `t` and the PSD function `sd`, we can generate a `SpectralRepresentation` object

```@example SpectralRepresentation
srm_obj = SpectralRepresentation(sd, t, :ShnzkSR)
```
To sample random phase angles, included in [eq:SRM](@ref) call

```@example AngleSampling
ϕ = sample(srm_obj)
```
φ is a `DataFrame` with row size `N` and the column size determined by the second argument of the function `sample`. By default it is $1$.

Finally, to retrieve a signal named $x$ in [eq:SRM](@ref) from , call

```@example Evaluate
x = evaluate(srm_obj, ϕ[1, :])
```